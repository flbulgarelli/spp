options {
   STATIC = false ;
}
PARSER_BEGIN(PredicateParser)
package pparser;
import java.io.ByteArrayInputStream;
import java.util.ArrayList;
import java.util.List;
import java.math.BigDecimal;

/**
 * A predicate-like syntax predicates parse
 * @author flbulgarelli
 */
public class PredicateParser {
  private EventHandler handler;
  private ValuesFactory factory;
  
  public void setEventHandler(EventHandler handler) {
    this.handler = handler;
  }

  /***
   * Parses the given expression, using a ValuesFactory for creating the values parsed, and
   * and EventHandler, for observing the predicates detection events the parses produces.
   * @throws IllegalArgumentException when the expression is not valid 
   */
  public static void parse(String expression, ValuesFactory factory, EventHandler handler) {
    try {
      PredicateParser parser = new PredicateParser(new ByteArrayInputStream(expression.getBytes()));
      parser.handler = handler;
      parser.factory = factory;
      parser.doParse();
    } catch (ParseException e) {
      throw new IllegalArgumentException("Syntax error in predicate expression " + expression + ". Cause: "
        + e.getMessage());
    }
  }
  
  /**
   * Validates this expression is parseable.
   *  
   *  It fails with and exception if not possible. Otherwise, does nothing.  
   */
  public static void tryParse(String expression) {
    parse(expression, new NullValuesFactory(), new NullEventHandler());
  }
  
}
PARSER_END(PredicateParser)


SKIP : { " " | "\n" | "\r" | "\t" }

TOKEN : { < COMMA : "," > }
TOKEN : { < AND : "and" > }
TOKEN : { < OR : "or" > }

TOKEN : { < DOT : "." > }

TOKEN : { < OPEN_PAR : "(" > }
TOKEN : { < CLOSE_PAR : ")" > }
TOKEN : { < OPEN_BRACKET : "[" > }
TOKEN : { < CLOSE_BRACKET : "]" > }

TOKEN : { < IDENTIFIER : ["a"-"z","A"-"Z", "_"](["a"-"z","A"-"Z","0"-"9"])* > }

TOKEN : { < NUMBER :  (["0"-"9"])+("."["0"-"9"])? > }
TOKEN : { < STRING :  "\"" (~["\""])* "\"" > }

TOKEN : { < EQ : "=" >  }
TOKEN : { < GT : ">" >  }
TOKEN : { < LT : "<" >  }
TOKEN : { < GTE : ">=" >  }
TOKEN : { < LTE : "<=" >  }
TOKEN : { < NEQ : "<>" | "!=" | "/=" | "\\=" >  }

TOKEN : { < PLUS : "+" > }
TOKEN : { < MINUS : "-" > }
TOKEN : { < MULT : "*" > }
TOKEN : { < DIV : "/" > }

PredicateOperator predicateOperator() :
{ PredicateOperator operator; }
{
  (  < EQ > { operator = PredicateOperator.EQ; }
  | < GT > { operator = PredicateOperator.GT; }
  | < LT > { operator = PredicateOperator.LT; } 
  | < LTE > { operator = PredicateOperator.LTE; } 
  | < GTE > { operator = PredicateOperator.GTE; } 
  | < NEQ > { operator = PredicateOperator.NEQ; })
   { return operator; }
}

ExpressionOperator expressionOperatorLevel1() :
{ ExpressionOperator operator; }
{
  ( < PLUS > { operator = ExpressionOperator.PLUS; }
  | < MINUS > { operator = ExpressionOperator.MINUS; })
  { return operator; }
}

ExpressionOperator expressionOperatorLevel2() :
{ ExpressionOperator operator; }
{
  ( < MULT > { operator = ExpressionOperator.MULT; }
  | < DIV > { operator = ExpressionOperator.DIV; })
   { return operator; }
}

void doParse() : 
{ }
{ 
   predicateLevel1() 
}

void predicateLevel1() :
{ }
{
   predicateLevel2() ( < OR > predicateLevel2() { handler.orPredicate(); } )* 
}

void predicateLevel2() :
{}
{
   predicateLevel3() ( < AND > predicateLevel3() { handler.andPredicate(); } )*
}

void predicateLevel3() :
{}
{ 
  LOOKAHEAD(< IDENTIFIER > < OPEN_PAR >)
  keywordPredicate()  |
  LOOKAHEAD(< IDENTIFIER > ( < DOT > < IDENTIFIER > )* (predicateOperator()) ) 
  operatorPredicate() | 
  idPredicate() |
  < OPEN_PAR > predicateLevel1() < CLOSE_PAR > 
}

void idPredicate() :
{
}
{
  path()
  { 
      handler.idPredicate();
  }
}


void keywordPredicate() :
{
  Token operation; 
  int arity = 1;
}
{
  operation = < IDENTIFIER >
  < OPEN_PAR >
  expressionLevel1()
  (< COMMA >
  expressionLevel1() { arity++; }
  (< COMMA >
  expressionLevel1() { arity++; })?)?
  < CLOSE_PAR > 
  { 
      handler.keywordPredicate(operation.image, arity);
  }
}

void operatorPredicate() :
{
  PredicateOperator operator; 
}
{
  expressionLevel1()
  (operator = predicateOperator() ) 
  expressionLevel1()
  { 
      handler.operatorPredicate(operator);
  }
}

void path() :
{ 
  List<String> routes = new ArrayList<String>();
  Token route = null; 
}
{
  route = < IDENTIFIER >
  {  routes.add(route.image); }
  (< DOT >
  route = < IDENTIFIER > 
  { routes.add(route.image); })*
  
  { handler.pathExpression(factory.createPath(routes)); }
}


void expressionLevel1() :
{ ExpressionOperator operator;  }
{
  expressionLevel2() (operator = expressionOperatorLevel1() expressionLevel2() {
     handler.operatorExpression(operator);
  })*
}

void expressionLevel2() :
{ ExpressionOperator operator;  }
{
  expressionLevel3() (operator = expressionOperatorLevel2() expressionLevel3() { 
     handler.operatorExpression(operator);
  })*
}

void expressionLevel3() :
{}
{
  (stringValue() |
   numericValue() |
   listValue() |
   path() )
}

void stringValue() :
{ Token v; }
{
  v =  <STRING>
  { handler.stringExpression(factory.createString(v.image)); }
}

void numericValue() :
{ Token v; }
{
  v =  <NUMBER>
  { handler.numberExpression(factory.createNumber(v.image)); }
}


void listValue() :
{  
  int size = 0; 
}
{	
	<OPEN_BRACKET>
	(expressionLevel1()
	{ size++; } 
	( < AND > expressionLevel1() 
	  { size++; }
	)*)?	
	<CLOSE_BRACKET>
	{ handler.listExpression(size); }
}

